plugins {
    id 'java'
    id 'war'
    id 'org.jbake.site' version '1.1.1'
    id 'org.akhikhl.gretty' version '2.0.0'
    id 'org.ajoberstar.git-publish' version '3.0.0-rc.1'
}

def outputDir = "$buildDir/jbake"

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

gitPublish {
    repoUri = 'git@github.com:jdpgrailsdev/blog.git'
    branch = 'gh-pages'

    contents {
        from(projectDir) {
            include 'CNAME'
        }
        from(file(outputDir)) {
            into '.'
        }
    }

    commitMessage = 'Updated blog content.'
}

gretty {
    httpPort = 8820
    contextPath = 'blog'
    servletContainer = 'jetty9'
}

jbake {
    clearCache = true
    asciidoctorjVersion = '1.5.6'
    freemarkerVersion = '2.3.23'
    pegdownVersion = '1.6.0'
    version = '2.6.0'
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

task copyContent(type: Copy) {
    from outputDir
    into "$buildDir/inplaceWebapp"
}

task publish {}

project.tasks.publish.dependsOn([project.tasks.bake, project.tasks.build, project.tasks.gitPublishPush])
project.tasks.gitPublishPush.outputs.upToDateWhen { false }

afterEvaluate { project ->
    // Re-generate content using JBake before launching the local Jetty server.
    project.tasks.jettyRun.dependsOn([project.tasks.bake, project.tasks.copyContent])
}