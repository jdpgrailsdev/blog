plugins {
    id 'java'
    id 'war'
    id 'org.jbake.site' version '1.0.0'
    id 'org.akhikhl.gretty' version '2.0.0'
    id 'org.ajoberstar.git-publish' version '0.3.0'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

gitPublish {
    repoUri = 'https://github.com/jdpgrailsdev/blog.git'
    branch = 'gh-pages'
    contents {
        from(file('build/jbake')) {
            into '.'
        }
    }
    commitMessage = 'Updating blog.'
}

gretty {
    httpPort = 8820
    contextPath = 'blog'
    servletContainer = 'jetty9'
}

jbake {
    srcDirName = 'src/jbake'
    destDirName = 'build/jbake'
    asciidoctorjVersion = '1.5.6'
    freemarkerVersion = '2.3.23'
    pegdownVersion = '1.6.0'
    version = '2.5.1'
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

task publish(dependsOn: 'gitPublishPush')

task copyContent(type: Copy) {
    from "$buildDir/jbake"
    into "$buildDir/inplaceWebapp"
}

afterEvaluate { project ->
    // Re-generate content using JBake before launching the local Jetty server.
    project.tasks.jettyRun.dependsOn([project.tasks.bake, project.tasks.copyContent])

    // Re-generate content using JBake before publishing to GitHub.
    project.tasks.publish.dependsOn([project.tasks.bake])
}