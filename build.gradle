buildscript {
    repositories {
        mavenLocal() // needed to use the local JBake 2.3.0 build
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'org.asciidoctor:asciidoctorj:1.5.1'
        classpath 'org.freemarker:freemarker:2.3.19'
        classpath 'org.jbake:jbake-core:2.5.0'
        classpath 'org.pegdown:pegdown:1.4.2'
    }
}

plugins {
    id 'java'
    id 'war'
    id 'me.champeau.jbake' version '0.2'
    id 'org.akhikhl.gretty' version '1.4.0'
    id 'org.ajoberstar.github-pages' version '1.5.1'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

githubPages {
    repoUri = 'https://github.com/jdpgrailsdev/blog.git'
    pages {
        from(file('build/jbake')) {
            into '.'
        }
    }
}

gretty {
    httpPort = 8820
    contextPath = 'blog'
    servletContainer = 'jetty9'
}

jbake {
    input = file('src/jbake')
    output = file('build/jbake')
    clearCache = true
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

task publish(dependsOn: 'publishGhPages')

task copyContent(type: Copy) {
    from "$buildDir/jbake"
    into "$buildDir/inplaceWebapp"
}

afterEvaluate { project ->
    // Re-generate content using JBake before launching the local Jetty server.
    project.tasks.jettyRun.dependsOn([project.tasks.jbake, project.tasks.copyContent])

    // Re-generate content using JBake before publishing to GitHub.
    project.tasks.publish.dependsOn([project.tasks.jbake])
}