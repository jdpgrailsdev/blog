plugins {
    id 'java'
    id 'war'
    id 'org.jbake.site' version '5.5.0'
    id 'org.gretty' version '4.0.0'
    id 'org.ajoberstar.git-publish' version '3.0.0'
}

def outputDir = "$buildDir/jbake"

sourceCompatibility = '11'
targetCompatibility = '11'

gitPublish {
    repoUri = 'git@github.com:jdpgrailsdev/blog.git'
    branch = 'gh-pages'

    contents {
        from(projectDir) {
            include 'CNAME'
        }
        from(file(outputDir)) {
            into '.'
        }
    }

    commitMessage = 'Updated blog content.'
}

gretty {
    inplaceMode = 'soft'
    httpPort = 8820
    contextPath = '/'
}

jbake {
    clearCache = true
    asciidoctorjVersion = '2.5.2'
    freemarkerVersion = '2.3.31'
    pegdownVersion = '1.6.0'
    version = '2.6.7'
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

task copyContent(type: Copy) {
    from outputDir
    into "$buildDir/inplaceWebapp"
}

task publish {}

project.tasks.build.dependsOn([project.tasks.bake])
project.tasks.copyContent.dependsOn([project.tasks.bake])
project.tasks.publish.dependsOn([project.tasks.build, project.tasks.gitPublishPush])
project.tasks.gitPublishPush.outputs.upToDateWhen { false }

afterEvaluate { project ->
    // Re-generate content using JBake before launching the local Jetty server.
    project.tasks.jettyRun.dependsOn([project.tasks.copyContent])
}